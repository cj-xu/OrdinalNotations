-------------------------------------------------------------
- Three equivalent ordinal notation systems in Cubical Agda -
-------------------------------------------------------------

Â§3.2  The mutual approach

We use Agdaâ€™s support for mutual definitions to directly generate
trees in Cantor normal form only, by simultaneously defining ordinal
terms and an ordering on them.

\begin{code}

{-# OPTIONS --cubical --safe #-}

module MutualOrd where

open import Preliminaries

\end{code}

â–  Ordinal notations and their ordering

\begin{code}

infix 30 _<_ _â‰¤_ _>_ _â‰¥_

data MutualOrd : Typeâ‚€
data _<_ : MutualOrd â†’ MutualOrd â†’ Typeâ‚€
fst : MutualOrd â†’ MutualOrd

_>_ _â‰¥_ _â‰¤_ : MutualOrd â†’ MutualOrd â†’ Typeâ‚€
a > b = b < a
a â‰¥ b = a > b âŠ a â‰¡ b
a â‰¤ b = b â‰¥ a

data MutualOrd where
 ğŸ : MutualOrd
 Ï‰^_+_[_] : (a b : MutualOrd) â†’ a â‰¥ fst b â†’ MutualOrd

private
 variable
  a b c d : MutualOrd
  r : a â‰¥ fst b
  s : c â‰¥ fst d

data _<_ where
 <â‚ : ğŸ < Ï‰^ a + b [ r ]
 <â‚‚ : a < c â†’ Ï‰^ a + b [ r ] < Ï‰^ c + d [ s ]
 <â‚ƒ : a â‰¡ c â†’ b < d â†’ Ï‰^ a + b [ r ] < Ï‰^ c + d [ s ]

fst  ğŸ               = ğŸ
fst (Ï‰^ a + _ [ _ ]) = a

rest : MutualOrd â†’ MutualOrd
rest  ğŸ               = ğŸ
rest (Ï‰^ _ + b [ _ ]) = b

caseMutualOrd : {A : Type â„“} (x y : A) â†’ MutualOrd â†’ A
caseMutualOrd x y  ğŸ               = x
caseMutualOrd x y (Ï‰^ _ + _ [ _ ]) = y

ğŸâ‰¢Ï‰ : {r : a â‰¥ fst b} â†’ Â¬ (ğŸ â‰¡ Ï‰^ a + b [ r ])
ğŸâ‰¢Ï‰ e = subst (caseMutualOrd MutualOrd âŠ¥) e ğŸ

Ï‰â‰¢ğŸ : {r : a â‰¥ fst b} â†’ Â¬ (Ï‰^ a + b [ r ] â‰¡ ğŸ)
Ï‰â‰¢ğŸ e = subst (caseMutualOrd âŠ¥ MutualOrd) e ğŸ

<-irrefl : Â¬ a < a
<-irrefl (<â‚‚ r)     = <-irrefl r
<-irrefl (<â‚ƒ a=a r) = <-irrefl r

<-irreflexive : a â‰¡ b â†’ Â¬ a < b
<-irreflexive {a} e = subst (Î» x â†’ Â¬ a < x) e <-irrefl

{----------------------------------------------------------------}
{-------- The following facts are proved simultaneously ---------}

<IsPropValued : isProp (a < b)
â‰¤IsPropValued : {a b : MutualOrd} â†’ isProp (a â‰¤ b)
MutualOrdIsDiscrete : Discrete MutualOrd
MutualOrdIsSet : isSet MutualOrd
MutualOrdâ¼ : {r : a â‰¥ fst b} {s : c â‰¥ fst d} â†’ a â‰¡ c â†’ b â‰¡ d
           â†’ Ï‰^ a + b [ r ] â‰¡ Ï‰^ c + d [ s ]

<IsPropValued  <â‚       <â‚       = refl
<IsPropValued (<â‚‚ p)   (<â‚‚ q)    = cong <â‚‚ (<IsPropValued p q)
<IsPropValued (<â‚‚ p)   (<â‚ƒ e _)  = âŠ¥-elim (<-irreflexive e p)
<IsPropValued (<â‚ƒ e _) (<â‚‚ q)    = âŠ¥-elim (<-irreflexive e q)
<IsPropValued (<â‚ƒ e p) (<â‚ƒ e' q) = congâ‚‚ <â‚ƒ (MutualOrdIsSet e e') (<IsPropValued p q)

â‰¤IsPropValued (injâ‚ p) (injâ‚ q) = cong injâ‚ (<IsPropValued p q)
â‰¤IsPropValued (injâ‚ p) (injâ‚‚ e) = âŠ¥-elim (<-irreflexive (e â»Â¹) p)
â‰¤IsPropValued (injâ‚‚ e) (injâ‚ q) = âŠ¥-elim (<-irreflexive (e â»Â¹) q)
â‰¤IsPropValued (injâ‚‚ p) (injâ‚‚ q) = cong injâ‚‚ (MutualOrdIsSet p q)

MutualOrdIsDiscrete  ğŸ                ğŸ               = yes refl
MutualOrdIsDiscrete  ğŸ               (Ï‰^ b + d [ s ]) = no ğŸâ‰¢Ï‰
MutualOrdIsDiscrete (Ï‰^ a + b [ r ])  ğŸ               = no Ï‰â‰¢ğŸ
MutualOrdIsDiscrete (Ï‰^ a + b [ r ]) (Ï‰^ c + d [ s ]) with MutualOrdIsDiscrete a c
MutualOrdIsDiscrete (Ï‰^ a + b [ r ]) (Ï‰^ c + d [ s ]) | yes aâ‰¡c with MutualOrdIsDiscrete b d
MutualOrdIsDiscrete (Ï‰^ a + b [ r ]) (Ï‰^ c + d [ s ]) | yes aâ‰¡c | yes bâ‰¡d = yes (MutualOrdâ¼ aâ‰¡c bâ‰¡d)
MutualOrdIsDiscrete (Ï‰^ a + b [ r ]) (Ï‰^ c + d [ s ]) | yes aâ‰¡c | no  bâ‰¢d = no (Î» e â†’ bâ‰¢d (cong rest e))
MutualOrdIsDiscrete (Ï‰^ a + b [ r ]) (Ï‰^ c + d [ s ]) | no  aâ‰¢c = no (Î» e â†’ aâ‰¢c (cong fst e))

{-- Inlining the proof of "Discreteâ†’isSet MutualOrdIsDiscrete" --}

â‰¡-normalise : a â‰¡ b â†’ a â‰¡ b
â‰¡-normalise {a} {b} aâ‰¡b with MutualOrdIsDiscrete a b
... | yes p = p
... | no Â¬p = âŠ¥-elim (Â¬p aâ‰¡b)

â‰¡-normalise-constant : (p q : a â‰¡ b) â†’ â‰¡-normalise p â‰¡ â‰¡-normalise q
â‰¡-normalise-constant {a} {b} p q with MutualOrdIsDiscrete a b
... | yes _ = refl
... | no Â¬p = âŠ¥-elim (Â¬p p)

â‰¡-canonical : (p : a â‰¡ b)
            â†’ (â‰¡-normalise refl) â»Â¹ âˆ™ (â‰¡-normalise p) â‰¡ p
â‰¡-canonical = J (Î» y p â†’ (â‰¡-normalise refl) â»Â¹ âˆ™ (â‰¡-normalise p) â‰¡ p)
                (lCancel (â‰¡-normalise refl))

MutualOrdIsSet p q =
  ((â‰¡-canonical p) â»Â¹) âˆ™
  cong ((â‰¡-normalise refl) â»Â¹ âˆ™_) (â‰¡-normalise-constant p q) âˆ™
  â‰¡-canonical q
{--  MutualOrdIsSet = Discreteâ†’isSet MutualOrdIsDiscrete _ _   --}

{--------------------- End of the inlining ----------------------}

import Agda.Builtin.Equality as P

MutualOrdâ¼ {a} {b} aâ‰¡c bâ‰¡d with PropEqfromPath aâ‰¡c | PropEqfromPath bâ‰¡d
... | P.refl | P.refl = cong (Ï‰^ a + b [_]) (â‰¤IsPropValued _ _)

{---------------- End of the simultaneous proofs ----------------}
{----------------------------------------------------------------}

\end{code}

Agda reports a termination error if we prove MutualOrdIsSet directly
using Discreteâ†’isSet from the cubical library.  So we have to inline
the proof of "Discreteâ†’isSet MutualOrdIsDiscrete".

Agda reports another termination error when we prove MutualOrdâ¼ using
the cubical subst function, e.g.

----------------- Begin of code -----------------

MutualOrdâ¼ {a} aâ‰¡c = subst P aâ‰¡c pa
 where
  P : MutualOrd â†’ Typeâ‚€
  P x = âˆ€ {b d r s} â†’ b â‰¡ d â†’ Ï‰^ a + b [ r ] â‰¡ Ï‰^ x + d [ s ]
  pa : P a
  pa {b} bâ‰¡d = subst Q bâ‰¡d qb
   where
    Q : MutualOrd â†’ Typeâ‚€
    Q y = âˆ€ {r s} â†’ Ï‰^ a + b [ r ] â‰¡ Ï‰^ a + y [ s ]
    qb : Q b
    qb = cong (Ï‰^ a + b [_]) (â‰¤IsPropValued _ _)

------------------ End of code ------------------

We instead convert paths to Agda's propositional equality which we can
pattern match on directly.

\begin{code}

<-tri : (a b : MutualOrd) â†’ a < b âŠ a â‰¥ b
<-tri  ğŸ                ğŸ               = injâ‚‚ (injâ‚‚ refl)
<-tri  ğŸ               (Ï‰^ b + d [ _ ]) = injâ‚ <â‚
<-tri (Ï‰^ a + c [ _ ])  ğŸ               = injâ‚‚ (injâ‚ <â‚)
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) with <-tri a b
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚       a<b  = injâ‚ (<â‚‚ a<b)
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚‚ (injâ‚ a>b) = injâ‚‚ (injâ‚ (<â‚‚ a>b))
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚‚ (injâ‚‚ a=b) with <-tri c d
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚‚ (injâ‚‚ a=b) | injâ‚       c<d  = injâ‚ (<â‚ƒ a=b c<d)
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚‚ (injâ‚‚ a=b) | injâ‚‚ (injâ‚ c>d) = injâ‚‚ (injâ‚ (<â‚ƒ (a=b â»Â¹) c>d))
<-tri (Ï‰^ a + c [ _ ]) (Ï‰^ b + d [ _ ]) | injâ‚‚ (injâ‚‚ a=b) | injâ‚‚ (injâ‚‚ c=d) = injâ‚‚ (injâ‚‚ (MutualOrdâ¼ a=b c=d))

<-trans : a < b â†’ b < c â†’ a < c
<-trans  <â‚      (<â‚‚ _)   = <â‚
<-trans  <â‚      (<â‚ƒ _ _) = <â‚
<-trans (<â‚‚ r)   (<â‚‚ s)   = <â‚‚ (<-trans r s)
<-trans (<â‚‚ r)   (<â‚ƒ p _) = <â‚‚ (subst (Î» x â†’ _ < x) p r)
<-trans (<â‚ƒ p _) (<â‚‚ s)   = <â‚‚ (subst (Î» x â†’ x < _) (p â»Â¹) s)
<-trans (<â‚ƒ p r) (<â‚ƒ q s) = <â‚ƒ (p âˆ™ q) (<-trans r s)

â‰¤-trans : a â‰¤ b â†’ b â‰¤ c â†’ a â‰¤ c
â‰¤-trans (injâ‚ a<b) (injâ‚ b<c) = injâ‚ (<-trans a<b b<c)
â‰¤-trans (injâ‚ a<b) (injâ‚‚ c=b) = injâ‚ (subst (Î» x â†’ _ < x) (c=b â»Â¹) a<b)
â‰¤-trans (injâ‚‚ b=a) (injâ‚ b<c) = injâ‚ (subst (Î» x â†’ x < _) b=a b<c)
â‰¤-trans (injâ‚‚ b=a) (injâ‚‚ c=b) = injâ‚‚ (c=b âˆ™ b=a)

<â‰¤-trans : a < b â†’ b â‰¤ c â†’ a < c
<â‰¤-trans a<b (injâ‚ b<c) = <-trans a<b b<c
<â‰¤-trans a<b (injâ‚‚ câ‰¡b) = subst (_ <_) (câ‰¡b â»Â¹) a<b

Lm[â‰¥â†’Â¬<] : a â‰¥ b â†’ Â¬ a < b
Lm[â‰¥â†’Â¬<] (injâ‚ b<a) a<b = <-irrefl (<-trans a<b b<a)
Lm[â‰¥â†’Â¬<] (injâ‚‚ a=b)     = <-irreflexive a=b


Lm[<â†’Â¬â‰¥] : a < b â†’ Â¬ a â‰¥ b
Lm[<â†’Â¬â‰¥] a<b (injâ‚ a>b) = <-irrefl (<-trans a<b a>b)
Lm[<â†’Â¬â‰¥] a<b (injâ‚‚ a=b) = <-irreflexive a=b a<b

Lm[Â¬<â†’â‰¥] : Â¬ a < b â†’ a â‰¥ b
Lm[Â¬<â†’â‰¥] f with <-tri _ _
Lm[Â¬<â†’â‰¥] f | injâ‚       a<b  = âŠ¥-elim (f a<b)
Lm[Â¬<â†’â‰¥] f | injâ‚‚ (injâ‚ a>b) = injâ‚ a>b
Lm[Â¬<â†’â‰¥] f | injâ‚‚ (injâ‚‚ a=b) = injâ‚‚ a=b

<-dec : (a b : MutualOrd) â†’ a < b âŠ Â¬ a < b
<-dec a b with <-tri a b
<-dec a b | injâ‚ a<b = injâ‚ a<b
<-dec a b | injâ‚‚ aâ‰¥b = injâ‚‚ (Lm[â‰¥â†’Â¬<] aâ‰¥b)

<-â‰¡ : {a b c : MutualOrd} â†’ a < b â†’ b â‰¡ c â†’ a < c
<-â‰¡ {a} e r = subst (a <_) r e

â‰¤â‰¥â†’â‰¡ : a â‰¤ b â†’ a â‰¥ b â†’ a â‰¡ b
â‰¤â‰¥â†’â‰¡ aâ‰¤b (injâ‚ a>b) = âŠ¥-elim (Lm[<â†’Â¬â‰¥] a>b aâ‰¤b)
â‰¤â‰¥â†’â‰¡ aâ‰¤b (injâ‚‚ a=b) = a=b

â‰®ğŸ : Â¬ a < ğŸ
â‰®ğŸ ()

â‰¥ğŸ : a â‰¥ ğŸ
â‰¥ğŸ {ğŸ}              = injâ‚‚ refl
â‰¥ğŸ {Ï‰^ a + b [ _ ]} = injâ‚ <â‚

fst< : (a b : MutualOrd) (r : a â‰¥ fst b)
     â†’ a < Ï‰^ a + b [ r ]
fst<  ğŸ               b r = <â‚
fst< (Ï‰^ a + c [ s ]) b r = <â‚‚ (fst< a c s)

rest< : (a b : MutualOrd) (r : a â‰¥ fst b)
      â†’ b < Ï‰^ a + b [ r ]
rest< a  ğŸ                _       = <â‚
rest< a (Ï‰^ b + c [ s ]) (injâ‚ r) = <â‚‚ r
rest< a (Ï‰^ b + c [ s ]) (injâ‚‚ e) = <â‚ƒ (e â»Â¹) (rest< b c s)

\end{code}

â–  Some simple examples of ordinal notations

\begin{code}

Ï‰^âŸ¨_âŸ© : MutualOrd â†’ MutualOrd
Ï‰^âŸ¨ a âŸ© = Ï‰^ a + ğŸ [ â‰¥ğŸ ]

ğŸ Ï‰ : MutualOrd
ğŸ = Ï‰^âŸ¨ ğŸ âŸ©
Ï‰ = Ï‰^âŸ¨ ğŸ âŸ©

\end{code}
