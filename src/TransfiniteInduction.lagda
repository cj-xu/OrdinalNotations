-------------------------------------------------------------
- Three equivalent ordinal notation systems in Cubical Agda -
-------------------------------------------------------------

Â§5  Transfinite induction

We prove transfinite induction for MutualOrd, and then transport it to
transfinite induction for HITOrd.  Then we consider a simple
application of transfinite induction - to prove that all strictly
descending sequences of ordinals below Îµâ‚€ are finite.

\begin{code}

{-# OPTIONS --cubical --safe #-}

module TransfiniteInduction where

open import Preliminaries
open import MutualOrd as M
open import HITOrd as H
open import Equivalences
open import Arithmetic

\end{code}

Â§5.1  The transported ordering on HITOrd

â–  Ordering on HITOrd

\begin{code}

_<á´´_ : HITOrd â†’ HITOrd â†’ Typeâ‚€
_<á´´_ = transport (Î» i â†’ Mâ‰¡H i â†’ Mâ‰¡H i â†’ Typeâ‚€) _<_

<Path : PathP (Î» i â†’ Mâ‰¡H i â†’ Mâ‰¡H i â†’ Typeâ‚€) _<_ _<á´´_
<Path i = transp (Î» j â†’ Mâ‰¡H (i âˆ§ j) â†’ Mâ‰¡H (i âˆ§ j) â†’ Typeâ‚€) (~ i) _<_

\end{code}

â–  Decidability of _<á´´_

\begin{code}

DEC : (A : Type â„“) â†’ (A â†’ A â†’ Type â„“') â†’ Type (â„“ âŠ” â„“')
DEC A _<_ = (x y : A) â†’ x < y âŠ Â¬ x < y

<á´´-dec : DEC HITOrd _<á´´_
<á´´-dec = transport (Î» i â†’ DEC (Mâ‰¡H i) (<Path i)) <-dec

\end{code}

â–  The transported property <á´´-dec computes.

\begin{code}

-- To simplify the examples, we turn <á´´-dec into a boolean-valued function.

open import Agda.Builtin.Bool

âŠ2Bool : {A : Type â„“} {B : Type â„“'} â†’ A âŠ B â†’ Bool
âŠ2Bool (injâ‚ _) = true
âŠ2Bool (injâ‚‚ _) = false

lt : HITOrd â†’ HITOrd â†’ Bool
lt a b = âŠ2Bool (<á´´-dec a b)

Ex[<á´´-decComp] :
   lt ğŸ ğŸ â‰¡ false
 Ã— lt H.Ï‰ ((H.ğŸ âŠ• H.ğŸ) âŠ— H.Ï‰) â‰¡ true
 Ã— lt H.Ï‰^âŸ¨ H.Ï‰ âŸ© H.Ï‰^âŸ¨ H.ğŸ +á´´ H.Ï‰ âŸ© â‰¡ false
 Ã— lt H.Ï‰^âŸ¨ H.Ï‰ âŸ© H.Ï‰^âŸ¨ H.ğŸ âŠ• H.Ï‰ âŸ© â‰¡ true
Ex[<á´´-decComp] = refl , refl , refl , refl

\end{code}

Â§5.2  Transfinite induction

â–  Principle of transfinite induction

\begin{code}

TI : (A : Type â„“) â†’ (A â†’ A â†’ Type â„“') â†’
     âˆ€ â„“'' â†’ Type (â„“ âŠ” â„“' âŠ” lsuc â„“'')
TI A _<_ â„“'' = (P : A â†’ Type â„“'')
             â†’ (âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x)
             â†’ âˆ€ x â†’ P x

\end{code}

â–  Accessibility

\begin{code}

module Acc (A : Type â„“) (_<_ : A â†’ A â†’ Type â„“') where

 data isAccessible (x : A) : Type (â„“ âŠ” â„“') where
  next : (âˆ€ y â†’ y < x â†’ isAccessible y) â†’ isAccessible x

 accInd : (P : A â†’ Type â„“'')
        â†’ (âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x)
        â†’ âˆ€ x â†’ isAccessible x â†’ P x
 accInd P step x (next Î´) =
   step x (Î» y r â†’ accInd P step y (Î´ y r))

\end{code}

â–  All elements of MutualOrd are accessible.

\begin{code}

open Acc MutualOrd _<_

ğŸAcc : isAccessible ğŸ
ğŸAcc = next (Î» x x<ğŸ â†’ âŠ¥-elim (â‰®ğŸ x<ğŸ))

-- fstAcc and sndAcc are proved simultaneously.

fstAcc : âˆ€ {a a'} â†’ isAccessible a' â†’ a â‰¡ a'
  â†’ âˆ€ {b x} â†’ isAccessible b â†’ x < a' â†’ (r : x â‰¥ fst b)
  â†’ isAccessible (Ï‰^ x + b [ r ])
sndAcc : âˆ€ {a a'} â†’ isAccessible a' â†’ a â‰¡ a'
  â†’ âˆ€ {c y} â†’ isAccessible c â†’ y < c â†’ (r : a â‰¥ fst y)
  â†’ isAccessible (Ï‰^ a + y [ r ])

fstAcc {a} {a'} (next Î¾) a=a' {b} {x} acáµ‡ x<a r = next goal
  where
   goal : âˆ€ z â†’ z < Ï‰^ x + b [ r ] â†’ isAccessible z
   goal ğŸ <â‚ = ğŸAcc
   goal (Ï‰^ c + d [ s ]) (<â‚‚ c<y) = fstAcc (Î¾ x x<a) refl (goal d (<-trans (rest< c d s) (<â‚‚ c<y))) c<y s
   goal (Ï‰^ c + d [ s ]) (<â‚ƒ c=y d<b) = sndAcc (Î¾ x x<a) c=y acáµ‡ d<b s

sndAcc {a} {a'} acáµƒ a=a' {c} {y} (next Î¾á¶œ) y<c r = next goal
  where
   goal : âˆ€ z â†’ z < Ï‰^ a + y [ r ] â†’ isAccessible z
   goal ğŸ <â‚ = ğŸAcc
   goal (Ï‰^ b + d [ t ]) (<â‚‚ b<a) = fstAcc acáµƒ a=a' (goal d (<-trans (rest< b d t) (<â‚‚ b<a))) (subst (b <_) a=a' b<a) t
   goal (Ï‰^ b + d [ t ]) (<â‚ƒ b=a d<y) = sndAcc acáµƒ (b=a âˆ™ a=a') (Î¾á¶œ y y<c) d<y t

Ï‰+Acc : (a b : MutualOrd) (r : a â‰¥ fst b)
      â†’ isAccessible a â†’ isAccessible b â†’ isAccessible (Ï‰^ a + b [ r ])
Ï‰+Acc a b r acáµƒ acáµ‡ = next goal
 where
  goal : âˆ€ z â†’ z < Ï‰^ a + b [ r ] â†’ isAccessible z
  goal ğŸ <â‚ = ğŸAcc
  goal (Ï‰^ c + d [ s ]) (<â‚‚ c<a) = fstAcc acáµƒ refl (goal d (<-trans (rest< c d s) (<â‚‚ c<a))) c<a s
  goal (Ï‰^ c + d [ s ]) (<â‚ƒ c=a d<b) = sndAcc acáµƒ c=a acáµ‡ d<b s

WF : (x : MutualOrd) â†’ isAccessible x
WF ğŸ = ğŸAcc
WF (Ï‰^ a + b [ r ]) = Ï‰+Acc a b r (WF a) (WF b)

\end{code}

â–  Transfinite induction for MutualOrd

\begin{code}

MTI : TI MutualOrd _<_ â„“
MTI P step x = accInd P step x (WF x)

\end{code}

â–  Transfinite induction for HITOrd

\begin{code}

HTI : TI HITOrd _<á´´_ â„“
HTI = transport (Î» i â†’ TI (Mâ‰¡H i) (<Path i) _) MTI

\end{code}

Â§5.3  All strictly descending sequences are finite

â–  Definitions

\begin{code}

pseudo-descending : (â„• â†’ MutualOrd) â†’ Typeâ‚€
pseudo-descending f =
  âˆ€ i â†’ f i > f (suc i) âŠ (f i â‰¡ ğŸ Ã— f (suc i) â‰¡ ğŸ)

strictly-descending : (â„• â†’ MutualOrd) â†’ Set
strictly-descending f = âˆ€ i â†’ f i > f (suc i)

eventually-zero : (â„• â†’ MutualOrd) â†’ Typeâ‚€
eventually-zero f = Î£ \(n : â„•) â†’ âˆ€ i â†’ i â‰¥á´º n â†’ f i â‰¡ ğŸ

\end{code}

â–  Some facts of pseudo-descendingness and eventual zeroness

\begin{code}

zeroPoint : âˆ€ {f} â†’ pseudo-descending f
  â†’ âˆ€ {i} â†’ f i â‰¡ ğŸ â†’ âˆ€ j â†’ j â‰¥á´º i â†’ f j â‰¡ ğŸ
zeroPoint df f0=0   0      zâ‰¤n                   = f0=0
zeroPoint df f0=0  (suc j) zâ‰¤n with df 0
zeroPoint df f0=0  (suc j) zâ‰¤n | injâ‚ f1<f0      = âŠ¥-elim (â‰®ğŸ (<-â‰¡ f1<f0 f0=0))
zeroPoint df f0=0  (suc j) zâ‰¤n | injâ‚‚ (_ , f1=0) = zeroPoint (df âˆ˜ suc) f1=0  j zâ‰¤n
zeroPoint df fsi=0 (suc j) (sâ‰¤s iâ‰¤j)             = zeroPoint (df âˆ˜ suc) fsi=0 j iâ‰¤j

nonzeroPoint : âˆ€ {f} â†’ pseudo-descending f
  â†’ âˆ€ {i} â†’ f i > ğŸ â†’ f i > f (suc i)
nonzeroPoint df fi>0 with df _
nonzeroPoint df fi>0 | injâ‚ fi+1<fi    = fi+1<fi
nonzeroPoint df fi>0 | injâ‚‚ (fi=0 , _) = âŠ¥-elim (â‰®ğŸ (<-â‰¡ fi>0 fi=0))

eventually-zero-cons :
  âˆ€ f â†’ eventually-zero (f âˆ˜ suc) â†’ eventually-zero f
eventually-zero-cons f (n , p) = suc n , p'
 where
  p' : (i : â„•) â†’ i â‰¥á´º suc n â†’ f i â‰¡ ğŸ
  p' (suc i) (sâ‰¤s r) = p i r

\end{code}

â–  All pseudo-descending sequences are eventually zero.

\begin{code}

PD2EZ : âˆ€ f â†’ pseudo-descending f â†’ eventually-zero f
PD2EZ f df = MTI P step (f 0) f df refl
 where
  P : MutualOrd â†’ Typeâ‚€
  P a = âˆ€ f â†’ pseudo-descending f â†’ f 0 â‰¡ a â†’ eventually-zero f
  step : âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x
  step x h f df f0=x with â‰¥ğŸ {f 0}
  step x h f df f0=x | injâ‚ f0>0 = goal
   where
    f1<x : f 1 < x
    f1<x = subst (f 1 <_) f0=x (nonzeroPoint df f0>0)
    ezfs : eventually-zero (f âˆ˜ suc)
    ezfs = h (f 1) f1<x (f âˆ˜ suc) (df âˆ˜ suc) refl
    goal : eventually-zero f
    goal = eventually-zero-cons f ezfs
  step x h f df f0=x | injâ‚‚ f0=0 = goal
   where
    fi=0 : âˆ€ i â†’ f i â‰¡ ğŸ
    fi=0 i = zeroPoint df f0=0 i zâ‰¤n
    goal : eventually-zero f
    goal = 0 , Î» i _ â†’ fi=0 i

\end{code}

â–  There is no strictly descending sequence.

\begin{code}

NSDS : âˆ€ f â†’ strictly-descending f â†’ âŠ¥
NSDS f sd = <-irreflexive fn+1=fn fn+1<fn
 where
  ez : eventually-zero f
  ez = PD2EZ f (injâ‚ âˆ˜ sd)
  n : â„•
  n = prâ‚ ez
  prf : âˆ€ i â†’ i â‰¥á´º n â†’ f i â‰¡ ğŸ
  prf = prâ‚‚ ez
  fn+1=fn : f (suc n) â‰¡ f n
  fn+1=fn = prf (suc n) nâ‰¤1+n âˆ™ (prf n â‰¤á´º-refl) â»Â¹
  fn+1<fn : f (suc n) < f n
  fn+1<fn = sd n

\end{code}
