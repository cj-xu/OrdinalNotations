
               -----------------------------
                   Transfinite induction 
               -----------------------------

                  Chuangjie Xu, June 2019


We tried to adapt Grimm's (Coq) proof to show the well-foundedness of
our ordinal notations.  The following two statements

 - if a and b are accessible and x < a, then Ï‰^x+b is accessible

 - if a and b are accessible and y < b, then Ï‰^a+y is accessible

are used as assumptions in Grimm's proof. But we cannot see why such
assumptions are allowed.  We instead consider them as lemmas and prove
them simultaneously.

From the well-foundedness, we derive the transfinite induction
principle for ğ’ª.  Then we use it to prove a stronger and computational
version of the well-known statement

  there is no infinite descending sequence of ordinals below Îµâ‚€.


References.

â–¡ JoseÌ Grimm. Implementation of three types of ordinals in Coq.
  Technical Report RR-8407, INRIA, 2013. Available at
  https://hal.inria.fr/hal-00911710.

\begin{code}

{-# OPTIONS --safe #-}

module TransfiniteInduction where

open import Agda.Builtin.Equality
open import Data.Empty.Irrelevant
open import Data.Sum using (injâ‚; injâ‚‚) renaming (_âŠ_ to _âˆ¨_)

open import OrdinalNotations

\end{code}

â–  Some lemmas

We can make irrelevant a â‰¥ b relevant because of the trichotomy.

\begin{code}

relevant : {a b : ğ’ª} â†’ .(a â‰¥ b) â†’ a â‰¥ b
relevant {a} {b} r with <-tri {a} {b}
...                | injâ‚ a<b = âŠ¥-elim (Lm[<â†’Â¬â‰¥] a<b r)
...                | injâ‚‚ aâ‰¥b = aâ‰¥b

fst< : âˆ€ a b â†’ .(r : a â‰¥ fst b) â†’ a < Ï‰^ a + b [ r ]
fst< ğŸ b r = <â‚ (Î» ())
fst< (Ï‰^ a + c [ s ]) b r = <â‚‚ (fst< a c s)

snd< : âˆ€ a b â†’ .(r : a â‰¥ fst b) â†’ b < Ï‰^ a + b [ r ]
snd< a ğŸ r = <â‚ (Î» ())
snd< a Ï‰^ b + d [ s ] r with relevant r
snd< a Ï‰^ b + d [ s ] r | injâ‚ a>b  = <â‚‚ a>b
snd< a Ï‰^ a + d [ s ] r | injâ‚‚ refl = <â‚ƒ (snd< a d (relevant s))

\end{code}

â–  Well-foundedness of ğ’ª

\begin{code}

data is-accessible (x : ğ’ª) : Set where
 next : (âˆ€ y â†’ y < x â†’ is-accessible y) â†’ is-accessible x

0-is-accessible : is-accessible ğŸ
0-is-accessible = next (Î» _ r â†’ âŠ¥-elim (Lm[â‰®0] r))

mutual

 --
 -- Grimm's assumption Hb
 --
 Lm[fst-acc] : âˆ€ a â†’ is-accessible a
             â†’ âˆ€ y b â†’ y < a â†’ .(r : y â‰¥ fst b) â†’ is-accessible b
             â†’ is-accessible (Ï‰^ y + b [ r ])
 Lm[fst-acc] a (next Î¾) y b y<a r acáµ‡ = next goal
  where
   goal : âˆ€ x â†’ x < Ï‰^ y + b [ r ] â†’ is-accessible x
   goal ğŸ (<â‚ _) = 0-is-accessible
   goal (Ï‰^ c + d [ s ]) (<â‚‚ c<y) = Lm[fst-acc] y (Î¾ y y<a) c d c<y s IH
    where
     IH : is-accessible d
     IH = goal d (<-trans (snd< c d s) (<â‚‚ c<y))
   goal (Ï‰^ a + d [ s ]) (<â‚ƒ d<b) = Lm[snd-acc] y (Î¾ y y<a) b acáµ‡ d d<b s

 --
 -- Grimm's assumption qb
 --
 Lm[snd-acc] : âˆ€ a â†’ is-accessible a
             â†’ âˆ€ c â†’ is-accessible c
             â†’ âˆ€ y â†’ y < c â†’ .(r : a â‰¥ fst y) â†’ is-accessible (Ï‰^ a + y [ r ])
 Lm[snd-acc] a acáµƒ c (next Î¾á¶œ) y y<c r = next goal
  where
   goal : âˆ€ x â†’ x < Ï‰^ a + y [ r ] â†’ is-accessible x
   goal ğŸ (<â‚ x) = 0-is-accessible
   goal (Ï‰^ b + d [ t ]) (<â‚‚ b<a) = Lm[fst-acc] a acáµƒ b d b<a t IH
    where
     IH : is-accessible d
     IH = goal d (<-trans (snd< b d t) (<â‚‚ b<a))
   goal (Ï‰^ a + d [ t ]) (<â‚ƒ d<y) = Lm[snd-acc] a acáµƒ y (Î¾á¶œ y y<c) d d<y t

--
-- ğ’ª is well-founded
--
WF : âˆ€ x â†’ is-accessible x
WF  ğŸ               = 0-is-accessible
WF (Ï‰^ a + b [ r ]) = next goal
 where
  goal : âˆ€ y â†’ y < Ï‰^ a + b [ r ] â†’ is-accessible y
  goal  ğŸ               (<â‚ _)   = 0-is-accessible
  goal (Ï‰^ c + d [ s ]) (<â‚‚ c<a) = Lm[fst-acc] a (WF a) c d c<a s IH
   where
    IH : is-accessible d
    IH = goal d (<-trans (snd< c d s) (<â‚‚ c<a))
  goal (Ï‰^ a + d [ s ]) (<â‚ƒ d<b) = Lm[snd-acc] a (WF a) b (WF b) d d<b s

\end{code}

â–  Transfinite induction for ğ’ª

\begin{code}

TIáµƒ : (P : ğ’ª â†’ Set)
    â†’ (âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x)
    â†’ âˆ€ x â†’ is-accessible x â†’ P x
TIáµƒ P step x (next Î´) = step x (Î» y r â†’ TIáµƒ P step y (Î´ y r))

TI : (P : ğ’ª â†’ Set)
   â†’ (âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x)
   â†’ âˆ€ x â†’ P x
TI P step x = TIáµƒ P step x (WF x)

\end{code}

â–  No infinite descending sequence of ordinals below Îµâ‚€

We prove a computational version using transfinite induction.

\begin{code}

open import Data.Nat using (â„• ; suc ; zâ‰¤n ; sâ‰¤s)
                     renaming (_â‰¥_ to _â‰¥á´º_ ; _>_ to _>á´º_ ; _<_ to _<á´º_ ; _â‰¤_ to _â‰¤á´º_)
open import Data.Nat.Properties using (â‰¤-refl ; nâ‰¤1+n)
open import Data.Product using (âˆƒ ; _,_ ; projâ‚ ; projâ‚‚) renaming (_Ã—_ to _âˆ§_)
open import Function using (_âˆ˜_)
open import Relation.Nullary using (Â¬_)
open import Relation.Binary.PropositionalEquality using (sym ; trans)

<-â‰¡ : {a b c : ğ’ª} â†’ a < b â†’ b â‰¡ c â†’ a < c
<-â‰¡ r refl = r

descending : (â„• â†’ ğ’ª) â†’ Set
descending f = âˆ€ i â†’ f i > f (suc i) âˆ¨ (f i â‰¡ ğŸ âˆ§ f (suc i) â‰¡ ğŸ)

descending-factâ‚€ : âˆ€{f} â†’ descending f
                 â†’ âˆ€{i} â†’ f i â‰¡ ğŸ â†’ âˆ€ j â†’ j â‰¥á´º i â†’ f j â‰¡ ğŸ
descending-factâ‚€ df f0=0   0      zâ‰¤n                   = f0=0
descending-factâ‚€ df f0=0  (suc j) zâ‰¤n with df 0
descending-factâ‚€ df f0=0  (suc j) zâ‰¤n | injâ‚ f1<f0      = âŠ¥-elim (Lm[â‰®0] (<-â‰¡ f1<f0 f0=0))
descending-factâ‚€ df f0=0  (suc j) zâ‰¤n | injâ‚‚ (_ , f1=0) = descending-factâ‚€ (df âˆ˜ suc) f1=0  j zâ‰¤n
descending-factâ‚€ df fsi=0 (suc j) (sâ‰¤s iâ‰¤j)             = descending-factâ‚€ (df âˆ˜ suc) fsi=0 j iâ‰¤j

descending-factâ‚ : âˆ€{f} â†’ descending f
                 â†’ âˆ€{i} â†’ f i > ğŸ â†’ f i > f (suc i)
descending-factâ‚ df fi>0 with df _
descending-factâ‚ df fi>0 | injâ‚ fi+1<fi    = fi+1<fi
descending-factâ‚ df fi>0 | injâ‚‚ (fi=0 , _) = âŠ¥-elim (Lm[â‰®0] (<-â‰¡ fi>0 fi=0))

eventually-zero : (â„• â†’ ğ’ª) â†’ Set
eventually-zero f = âˆƒ \n â†’ âˆ€ i â†’ i â‰¥á´º n â†’ f i â‰¡ ğŸ

P : ğ’ª â†’ Set
P a = (f : â„• â†’ ğ’ª) â†’ descending f â†’ f 0 â‰¡ a â†’ eventually-zero f

step : âˆ€ x â†’ (âˆ€ y â†’ y < x â†’ P y) â†’ P x
step .(f 0) h f df refl with Lm[â‰¥ğŸ] {f 0}
step .(f 0) h f df refl | injâ‚ f0>0 = suc n , goal
 where
  claim : eventually-zero (f âˆ˜ suc)
  claim = h (f 1) (descending-factâ‚ df f0>0) (f âˆ˜ suc) (df âˆ˜ suc) refl
  n : â„•
  n = projâ‚ claim
  goal : âˆ€ i â†’ i â‰¥á´º suc n â†’ f i â‰¡ ğŸ
  goal 0 ()
  goal (suc i) (sâ‰¤s r) = projâ‚‚ claim i r
step .(f 0) h f df refl | injâ‚‚ f0=0 = 0 , Î» i _ â†’ descending-factâ‚€ df f0=0 i zâ‰¤n

Theorem : (f : â„• â†’ ğ’ª) â†’ descending f â†’ eventually-zero f
Theorem f df = TI P step (f 0) f df refl

strictly-descending : (â„• â†’ ğ’ª) â†’ Set
strictly-descending f = âˆ€ i â†’ f i > f (suc i)

Corollary : Â¬ (âˆƒ \(f : â„• â†’ ğ’ª) â†’ strictly-descending f)
Corollary (f , sd) = <-irrefl' fn+1=fn fn+1<fn
 where
  ez : eventually-zero f
  ez = Theorem f (injâ‚ âˆ˜ sd)
  n : â„•
  n = projâ‚ ez
  pr : âˆ€ i â†’ i â‰¥á´º n â†’ f i â‰¡ ğŸ
  pr = projâ‚‚ ez
  fn+1=fn : f (suc n) â‰¡ f n
  fn+1=fn = trans (pr (suc n) (nâ‰¤1+n n)) (sym (pr n â‰¤-refl))
  fn+1<fn : f (suc n) < f n
  fn+1<fn = sd n
  <-irrefl' : {a b : ğ’ª} â†’ a â‰¡ b â†’ Â¬ (a < b)
  <-irrefl' refl = <-irrefl

\end{code}
