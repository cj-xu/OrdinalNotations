
      ---------------------------------------------------
        An equivalent inductive-inductive definition of
                      ordinal notations
      ---------------------------------------------------

            Fredrik Nordvall Forsberg, April 2019


The metatheory of inductive-recursive definitions (A, f), where the
recursive function f : A â†’ A is an endofunction on the inductively
defined type A (e.g. (ğ’ª, fst) here) is in general not well explored.
However, in this case fst is only used strictly positively in ğ’ª and <,
which means that its graph can be defined inductive-inductively, and
in turn used instead of the recursively defined fst.  This reduces the
inductive-inductive-recursive construction to an inductive-inductive
one, which is known to be sound.

\begin{code}

module OrdinalNotationsWithoutRecursion where

open import Agda.Builtin.Equality
open import Data.Empty.Irrelevant
open import Relation.Nullary using (Â¬_)
open import Relation.Binary.PropositionalEquality using (_â‰¢_; congâ‚‚)
open import Data.Sum using (injâ‚; injâ‚‚) renaming (_âŠ_ to _âˆ¨_)
open import Data.Product

import OrdinalNotations as O

infix 30 _<_
infix 30 _â‰¥fst_

\end{code}

We inductively define the type of ordinal notations ğ’ª, the order
relation _<_ on ğ’ª, and the relation _â‰¡fst_ representing the graph of
the function fst simultaneously. This is thus an instance of an
inductive-inductive definition, since the latter relations are indexed
by the former type, and they all refer to each other.

\begin{code}

mutual

 data ğ’ª : Set where
  ğŸ : ğ’ª
  Ï‰^_+_[_] : (a b : ğ’ª) â†’ .(a â‰¥fst b) â†’ ğ’ª

 data _<_ : ğ’ª â†’ ğ’ª â†’ Set where
  <â‚ : {a : ğ’ª}
     â†’ a â‰¢ ğŸ â†’ ğŸ < a
  <â‚‚ : {a b c d : ğ’ª} .{r : a â‰¥fst c} .{s : b â‰¥fst d}
     â†’ a < b â†’ Ï‰^ a + c [ r ] < Ï‰^ b + d [ s ]
  <â‚ƒ : {a b c : ğ’ª} .{r : a â‰¥fst b} .{s : a â‰¥fst c}
     â†’ b < c â†’ Ï‰^ a + b [ r ] < Ï‰^ a + c [ s ]

 -- The graph of fst
 data _â‰¡fst_ : ğ’ª â†’ ğ’ª â†’ Set where
   -- fst ğŸ = ğŸ
   fstğŸ         : ğŸ â‰¡fst ğŸ
   -- fst (Ï‰^ b + d [ s ]) = b
   fst[Ï‰^b+[â€¦]] : {b d : ğ’ª} .{s : b â‰¥fst d}
                â†’ b â‰¡fst (Ï‰^ b + d [ s ])

 fst_<_ : ğ’ª â†’ ğ’ª â†’ Set
 fst a < b = Î£[ c âˆˆ ğ’ª ] ((c â‰¡fst a) Ã— c < b)

 _â‰¥fst_ : ğ’ª â†’ ğ’ª â†’ Set
 a â‰¥fst b = fst b < a âˆ¨ a â‰¡fst b
\end{code}

Note that the last two definitions are abbreviations for convenience
only, since they make no use of recursion or pattern matching.

We can now define fst as an ordinary function, after the definition of
ğ’ª, and show that the graph relation really represents the graph of
fst.

\begin{code}

fst : ğ’ª â†’ ğ’ª
fst  ğŸ               = ğŸ
fst (Ï‰^ a + _ [ _ ]) = a

sound-â‰¡fst : (a b : ğ’ª) -> a â‰¡fst b -> a â‰¡ fst b
sound-â‰¡fst .ğŸ ğŸ                fstğŸ          = refl
sound-â‰¡fst a .(Ï‰^ a + _ [ _ ]) fst[Ï‰^b+[â€¦]] = refl

complete-â‰¡fst : (a b : ğ’ª) -> a â‰¡ fst b -> a â‰¡fst b
complete-â‰¡fst .ğŸ ğŸ                refl = fstğŸ
complete-â‰¡fst .b (Ï‰^ b + c [ r ]) refl = fst[Ï‰^b+[â€¦]]

\end{code}

Similarly, we can show that fst_<_ adequately represents the relation
Î» a b â†’ (fst a) < b:

\begin{code}

sound-fst< : (a b : ğ’ª) -> fst_<_ a b -> (fst a) < b
sound-fst< .ğŸ                b (.ğŸ , fstğŸ , q)         = q
sound-fst< .(Ï‰^ d + _ [ _ ]) b (d , fst[Ï‰^b+[â€¦]] , q) = q

complete-fst< : (a b : ğ’ª) -> (fst a) < b -> fst_<_ a b
complete-fst< ğŸ                b p = ğŸ , fstğŸ , p
complete-fst< (Ï‰^ a + c [ r ]) b p = a , fst[Ï‰^b+[â€¦]] , p

\end{code}

We can prove that ğ’ª defined simultaneously with fst is isomorphic to ğ’ª
defined simultaneously with the graph of fst. Because of the
simultaneous definition of ğ’ª and <, we have to prove that the
functions between the types preserve the relation < at the same time.

\begin{code}

mutual

  to : O.ğ’ª -> ğ’ª
  to O.ğŸ                = ğŸ
  to (O.Ï‰^ a + b [ r ]) = Ï‰^ (to a) + (to b) [ to-â‰¥ r ]

  to-< : {a b : O.ğ’ª} -> a O.< b -> (to a) < (to b)
  to-< {b = O.ğŸ}              (O.<â‚ p) = âŠ¥-elim (p refl)
  to-< {b = O.Ï‰^ a + c [ r ]} (O.<â‚ p) = <â‚ (Î» ())
  to-<                        (O.<â‚‚ p) = <â‚‚ (to-< p)
  to-<                        (O.<â‚ƒ p) = <â‚ƒ (to-< p)

  to-â‰¥ : {a b : O.ğ’ª} -> a O.â‰¥ O.fst b -> (to a) â‰¥fst (to b)
  to-â‰¥ {b = O.ğŸ}               (injâ‚ p)    = injâ‚ (_ , fstğŸ , to-< p)
  to-â‰¥ {b = O.Ï‰^ b + bâ‚ [ x ]} (injâ‚ p)    = injâ‚ (_ , fst[Ï‰^b+[â€¦]] , to-< p)
  to-â‰¥ {b = O.ğŸ}               (injâ‚‚ refl) = injâ‚‚ fstğŸ
  to-â‰¥ {b = O.Ï‰^ b + c [ r ]}  (injâ‚‚ refl) = injâ‚‚ (fst[Ï‰^b+[â€¦]] {s = to-â‰¥ r})

mutual

  from : ğ’ª -> O.ğ’ª
  from ğŸ                = O.ğŸ
  from (Ï‰^ a + b [ r ]) = O.Ï‰^ (from a) + (from b) [ from-â‰¥ r ]

  from-< : {a b : ğ’ª} -> a < b -> (from a) O.< (from b)
  from-< {b = ğŸ}              (<â‚ p) = âŠ¥-elim (p refl)
  from-< {b = Ï‰^ b + c [ r ]} (<â‚ p) = O.<â‚ (Î» ())
  from-<                      (<â‚‚ p) = O.<â‚‚ (from-< p)
  from-<                      (<â‚ƒ p) = O.<â‚ƒ (from-< p)

  from-â‰¥ : {a b : ğ’ª} -> a â‰¥fst b -> (from a) O.â‰¥ O.fst (from b)
  from-â‰¥ {b = ğŸ}              (injâ‚ (ğŸ , fstğŸ , p))           = injâ‚ (from-< p)
  from-â‰¥ {b = Ï‰^ b + c [ r ]} (injâ‚ (.b , fst[Ï‰^b+[â€¦]] , p)) = injâ‚ (from-< p)
  from-â‰¥                      (injâ‚‚ fstğŸ)                     = injâ‚‚ refl
  from-â‰¥ (injâ‚‚ fst[Ï‰^b+[â€¦]])                                 = injâ‚‚ refl

\end{code}

An isomorphism is especially easy to establish, because of the
irrelevant equations.

\begin{code}

Ï‰^_+_[_]-equal : âˆ€ {a b a' b'} .{r r'} â†’ (a â‰¡ a') â†’ (b â‰¡ b') â†’
                  (ğ’ª.Ï‰^ a + b [ r ]) â‰¡ (Ï‰^ a' + b' [ r' ])
Ï‰^_+_[_]-equal refl refl = refl

OÏ‰^_+_[_]-equal : âˆ€ {a b a' b'} .{r r'} â†’ (a â‰¡ a') â†’ (b â‰¡ b') â†’
                 (O.ğ’ª.Ï‰^ a + b [ r ]) â‰¡ (O.Ï‰^ a' + b' [ r' ])
OÏ‰^_+_[_]-equal refl refl = refl

to-from : (a : ğ’ª) -> to (from a) â‰¡ a
to-from ğŸ                = refl
to-from (Ï‰^ a + b [ r ]) = Ï‰^_+_[_]-equal (to-from a) (to-from b)

from-to : (b : O.ğ’ª) -> from (to b) â‰¡ b
from-to O.ğŸ                = refl
from-to (O.Ï‰^ a + b [ r ]) = OÏ‰^_+_[_]-equal (from-to a) (from-to b)

\end{code}
